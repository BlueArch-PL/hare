// Program
program = _{ SOI ~ ( block | statement )* ~ EOI }

// Block
block = { "{" ~ statement* ~ "}" }

// Statements
statement = {
    (
        assign_statement
        | set_value_statement
        | return_block_statement
        | if_statement
        | expr
    )
    ~ ";"?
}
assign_statement = { "let " ~ ident ~ ( ":" ~ ident)? ~ "=" ~ expr }
set_value_statement = { ident ~ "=" ~ expr }
return_block_statement = { "rtb " ~ expr }
if_statement = {
    "if " ~ expr ~ block
    ~ elif_statement*
    ~ else_statement?
}
elif_statement = { "elif " ~ expr ~ block }
else_statement = { "else " ~ block }

// Expressions
expr = { term ~ (bin_op ~ term)* }
term = _{ ( "(" ~ expr ~ ")" ) | constant | ident }
bin_op = _{ 
    add
    | subtract
    | multiply
    | divide
    | modulo 
    | equals 
    | not_equals
    | greater_than_or_equal_to 
    | less_than_or_equal_to  
    | greater_than 
    | less_than 
}

add = @{ "+" }
subtract = @{ "-" }
multiply = @{ "*" }
divide = @{ "/" }
modulo = @{ "%" }
equals = @{ "==" }
not_equals = @{ "!=" }
greater_than = @{ ">" }
less_than = @{ "<" }
greater_than_or_equal_to = @{ ">=" }
less_than_or_equal_to = @{ "<=" }

// Identifiers
ident = @{ (LETTER | "_" | NUMBER)+ }

// Constants
constant = { int | float | string | boolean }
int = @{NUMBER+}
float = @{"0." | (NUMBER)+ ~ "." ~ (NUMBER)+}
string = @{"\"" ~ LETTER* ~ "\""}
boolean = @{"true" | "false"}

WHITESPACE = _{ " " | "\n" | "\t"  }
COMMENT = _{ "#" ~ (!"\n" ~ ANY)* }